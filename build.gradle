plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.github.johnrengelman.shadow"


    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.layered() {
            it.officialMojangMappings {
                setNameSyntheticMembers(false)
            }
            it.parchment("org.parchmentmc.data:parchment-${rootProject.parchment_version}")
        }
    }

    var env = System.getenv()
    var modLoader = name

    var changelogFile = rootProject.file("changelog.md")
    var changelogText = changelogFile.exists() ? changelogFile.text : ""

    if (env.MODRINTH_TOKEN) {
        modrinth {
            projectId = "every-compat"
            uploadFile = tasks.remapJar
            additionalFiles.add(tasks.remapSourcesJar)
            gameVersions = [rootProject.minecraft_version]
            loaders = [modLoader]
            changelog = changelogText

            if (modLoader == "fabric") {
                versionName = mod_version + '-fabric'
                dependencies {
                    required.project "fabric-api"
                    required.project "moonlight"
                }
            } else {
                versionName = mod_version + '-forge'
                dependencies {
                    required.project "moonlight"
                }
            }
        }
    }

    if (env.CURSEFORGE_TOKEN) {
        curseforge {
            apiKey = env.CURSEFORGE_TOKEN
            project {
                id = rootProject.project_id
                changelog = changelogText
                changelogType = 'markdown'
                releaseType = 'release'
                addGameVersion rootProject.minecraft_version
                addGameVersion modLoader.capitalize()

                mainArtifact(tasks.remapJar)

                if (modLoader == "fabric") {
                    relations {
                        requiredDependency "fabric-api"
                        requiredDependency "selene"
                        optionalDependency("another-furniture")
                        optionalDependency("architects-palette-fabric")
                        optionalDependency("backpacked")
                        optionalDependency("building-but-better")
                        optionalDependency("camp-chair")
                        optionalDependency("chipped")
                        optionalDependency("clutter")
                        optionalDependency("create-fabric")
                        optionalDependency("decorative-blocks")
                        optionalDependency("dramatic-doors")
                        optionalDependency("exlines-awnings")
                        optionalDependency("exlines-bark-carpets")
                        optionalDependency("farmers-delight-fabric")
                        optionalDependency("farmers-delight-refabricated")
                        optionalDependency("friends-and-foes")
                        optionalDependency("furnish")
                        optionalDependency("handcrafted")
                        optionalDependency("hearth-and-home")
                        optionalDependency("infinitybuttons")
                        optionalDependency("lightmans-currency-fabric")
                        optionalDependency("macaws-bridges")
                        optionalDependency("macaws-doors")
                        optionalDependency("macaws-fences-and-walls")
                        optionalDependency("macaws-furniture")
                        optionalDependency("macaws-lights-and-lamps")
                        optionalDependency("macaws-paths-and-pavings")
                        optionalDependency("macaws-roofs")
                        optionalDependency("macaws-trapdoors")
                        optionalDependency("macaws-windows")
                        optionalDependency("mighty-mail-fabric")
                        optionalDependency("missing-wilds")
                        optionalDependency("more-crafting-tables-lieonlion")
                        optionalDependency("quark")
                        optionalDependency("regions-unexplored")
//                        optionalDependency("the-twilight-forest") //!! NOT AVAILABLE
                        optionalDependency("table-top-craft-fabric")
                        optionalDependency("twigs")
                        optionalDependency("valhelsia-furniture")
                        optionalDependency("variant-vanilla-blocks")
                        optionalDependency("villagersplus-fabric")
                        optionalDependency("wooden-hoppers")
                    }
                } else {
                    relations {
                        requiredDependency("selene")
                        optionalDependency("sawmill")
                        optionalDependency("absent-by-design")
                        optionalDependency("another-furniture")
                        optionalDependency("architects-palette")
                        optionalDependency("backpacked")
                        optionalDependency("boatload")
                        optionalDependency("buildersaddition")
                        optionalDependency("building-but-better")
                        optionalDependency("camp-chair")
                        optionalDependency("chipped")
                        optionalDependency("corail-pillar")
                        optionalDependency("create")
                        optionalDependency("dawn-of-time")
                        optionalDependency("decorative-blocks")
                        optionalDependency("decoration-delight")
                        optionalDependency("domum-ornamentum")
                        optionalDependency("dramatic-doors")
                        optionalDependency("exlines-bark-carpets")
                        optionalDependency("farmers-delight")
                        optionalDependency("friends-and-foes-forge")
                        optionalDependency("functional-storage")
                        optionalDependency("furnish")
                        optionalDependency("handcrafted")
                        optionalDependency("hearth-and-home")
                        optionalDependency("infinitybuttons")
                        optionalDependency("just-a-raft-mod")
                        optionalDependency("lightmans-currency")
                        optionalDependency("macaws-bridges")
                        optionalDependency("macaws-doors")
                        optionalDependency("macaws-fences-and-walls")
                        optionalDependency("macaws-furniture")
                        optionalDependency("macaws-lights-and-lamps")
                        optionalDependency("macaws-paths-and-pavings")
                        optionalDependency("macaws-roofs")
                        optionalDependency("macaws-trapdoors")
                        optionalDependency("macaws-windows")
                        optionalDependency("mighty-mail")
                        optionalDependency("missing-wilds-forge")
                        optionalDependency("more-chest-variants-lieonlion")
                        optionalDependency("more-crafting-tables-for-forge")
                        optionalDependency("more-crafting-tables-lieonlion")
                        optionalDependency("mosaic-carpentry")
                        optionalDependency("mrcrayfish-furniture-mod")
                        optionalDependency("pokecube-aoi")
                        optionalDependency("premium-wood")
//                        optionalDependency("productivebees")
                        optionalDependency("quark")
                        optionalDependency("redeco")
                        optionalDependency("regions-unexplored")
                        optionalDependency("storage-drawers")
                        optionalDependency("timber-frames")
                        optionalDependency("the-graveyard-forge")
                        optionalDependency("the-twilight-forest")
                        optionalDependency("table-top-craft")
                        optionalDependency("tropicraft")
                        optionalDependency("twigs")
                        optionalDependency("valhelsia-furniture")
                        optionalDependency("variant-crafting-tables")
                        optionalDependency("variant-vanilla-blocks")
                        optionalDependency("villagersplus-forge")
                        optionalDependency("woodster")
                        optionalDependency("woodworks")
                        optionalDependency("workshop-for-handsome-adventurer")
                        optionalDependency("xercamod")
                    }
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.mod_id
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {

        mavenLocal()

        flatDir {
            dirs 'mods'
        }

        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url "https://maven.parchmentmc.org" }

        maven {
            url = "https://www.cursemaven.com"
            allowInsecureProtocol = true
        }
        maven { url = "https://api.modrinth.com/maven" }

        maven { url = "https://mvn.devos.one/snapshots/" }

        maven { url = "https://mvn.devos.one/releases" } // Porting Lib Releases

        maven { url "https://maven.shedaniel.me/" } // Cloth Config

        // Required for Create
        maven {
            name "tterrag maven"
            url "https://maven.tterrag.com/" // Create & Several Dependencies
        }
        maven { // Forge Config API Port
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven { // Reach Entity Attributes
            url = "https://maven.jamieswhiteshirt.com/libs-release"
            content { includeGroup("com.jamieswhiteshirt") }
        }
        maven { // CC: Tweaked
            url "https://squiddev.cc/maven"
        }
        maven { // Unknown
            name = 'Ladysnake Mods'
            url = 'https://maven.ladysnake.org/releases'
        }
        maven { // JEI
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven { // Botania & FALLBACK for JEI
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
        // END -------------
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
